buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
        if (project.hasProperty('developPlugins')) {
            mavenLocal()
        } else {
            maven { url 'https://jitpack.io' }
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.liquibase:liquibase-gradle-plugin:${gradlePluginVersion}")
        classpath("org.postgresql:postgresql:${postgreSqlVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorPluginVersion}")
        if (project.hasProperty('developPlugins')) {
            classpath("com.epam.petclinic.plugin:petclinic-plugins:${pluginsVersionToUse}")
        } else {
            classpath("com.github.shalauniou:petclinic-plugins:${pluginsVersionToUse}")
        }
    }
}

apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'liquibase'
apply plugin: 'groovy'
apply plugin: 'project-report'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'petclinic-java'
apply plugin: 'petclinic-artifactory'

/*version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8*/

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-actuator-docs')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.boot:spring-boot-starter-validation')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.projectlombok:lombok')
    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('org.postgresql:postgresql')
    runtime('com.h2database:h2')
    testCompile('org.liquibase:liquibase-core')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.RELEASE'
    }
}

jar {
    dependsOn asciidoctor
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    systemProperty 'spring.profiles.active', 'test'
}

[processResources, processTestResources]*.doFirst {
    expand(project.properties)
}

liquibase {
    activities {
        main {
            changeLogFile "src/main/resources/${changeLogPath}"
            url "${jdbcURL}${databaseName}"
            defaultSchemaName "${schemaName}"
            username "${username}"
            password "${password}"
        }
    }
}

/*checkstyle {
    toolVersion = '7.1'
    configProperties.suppressionsFile = rootProject.file('/checkstyle/checkstyle-suppressions.xml')
    configFile = rootProject.file('/checkstyle/checkstyle-rules.xml')
}*/

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/resources/asciidoc'
}

apply from: 'tasks.gradle'

